/**
 * Copyright 2015 Scott Mansfield
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.widowcrawler'
sourceCompatibility = 1.8
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    compile 'joda-time:joda-time:2.7'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'stax:stax:1.2.0'

    // For pulling the sitemaps
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'
    compile 'org.glassfish.jersey.core:jersey-client:2.17'

    // logging
    compile 'org.slf4j:slf4j-api:1.7.12'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testRuntime 'org.slf4j:slf4j-simple:1.7.12'
}

// Publishing bits

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Exo'
                packaging 'jar'
                description 'Exo is a parser for sitemap.xml files.'
                url 'https://github.com/ScottMansfield/exo'

                scm {
                    url 'https://github.com/ScottMansfield/exo'
                    connection 'scm:git@github.com:ScottMansfield/exo.git'
                    developerConnection 'scm:git@github.com:ScottMansfield/exo.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ScottMansfield'
                        name 'Scott Mansfield'
                        email 'sgmansf@gmail.com'
                    }
                }
            }
        }
    }
}